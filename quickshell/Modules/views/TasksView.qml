import QtQuick
import Quickshell
import qs.Common
import qs.Services
import qs.Widgets
import qs.DankCommon.Widgets

Item {
    id: root

    property date displayDate: new Date()
    property date today: new Date()
    property int tasksVersion: 0

    signal taskClicked(var task)
    signal createTaskRequested

    Connections {
        target: DankCalService
        function onTasksUpdated() {
            root.tasksVersion++;
        }
    }

    function dueLabel(task) {
        if (!task.due)
            return "";
        const todayStart = new Date(root.today.getFullYear(), root.today.getMonth(), root.today.getDate());
        const diff = Math.round((task.due.getTime() - todayStart.getTime()) / 86400000);
        let dateLabel = "";
        switch (diff) {
        case 0:
            dateLabel = I18n.tr("Today", "due-date label on a task card");
            break;
        case 1:
            dateLabel = I18n.tr("Tomorrow", "due-date label on a task card");
            break;
        case -1:
            dateLabel = I18n.tr("Yesterday", "due-date label on a task card");
            break;
        default:
            dateLabel = SettingsData.formatDate(task.due, "MMM d");
        }
        return task.allDay ? dateLabel : dateLabel + " · " + SettingsData.formatTime(task.due);
    }

    function taskOverdue(task) {
        return !!task.due && !task.completed && task.due.getTime() < Date.now();
    }

    readonly property var sections: {
        tasksVersion;
        today;
        const buckets = DankCalService.taskBuckets();
        const order = [
            {
                key: "overdue",
                label: I18n.tr("Overdue", "tasks view section header for past-due tasks"),
                overdue: true
            },
            {
                key: "today",
                label: I18n.tr("Today", "tasks view section header for tasks due today"),
                overdue: false
            },
            {
                key: "upcoming",
                label: I18n.tr("Upcoming", "tasks view section header for tasks due later"),
                overdue: false
            },
            {
                key: "someday",
                label: I18n.tr("No due date", "tasks view section header for tasks without a due date"),
                overdue: false
            }
        ];
        const out = [];
        for (let i = 0; i < order.length; i++) {
            const items = buckets[order[i].key];
            if (items.length === 0)
                continue;
            out.push({
                "label": order[i].label,
                "overdue": order[i].overdue,
                "tasks": items
            });
        }
        const completed = DankCalService.visibleTasks(true).filter(t => t.completed);
        if (completed.length > 0)
            out.push({
                "label": I18n.tr("Completed", "tasks view section header for finished tasks"),
                "overdue": false,
                "tasks": completed
            });
        return out;
    }

    DankButton {
        id: addButton
        anchors.top: parent.top
        anchors.right: parent.right
        text: I18n.tr("Add task", "tasks view button to create a new task")
        iconName: "add"
        buttonHeight: 36
        backgroundColor: Theme.primary
        textColor: Theme.primaryText
        enabled: DankCalService.taskListCalendars().length > 0
        onClicked: root.createTaskRequested()
    }

    DankFlickable {
        anchors.top: addButton.bottom
        anchors.topMargin: Theme.spacingM
        anchors.left: parent.left
        anchors.right: parent.right
        anchors.bottom: parent.bottom
        contentWidth: width
        contentHeight: tasksColumn.implicitHeight
        clip: true

        Column {
            id: tasksColumn
            width: root.width
            spacing: Theme.spacingM

            StyledText {
                visible: root.sections.length === 0
                text: DankCalService.connected ? I18n.tr("No tasks yet", "tasks view empty state") : I18n.tr("Waiting for the dankcalendar daemon...", "tasks view placeholder while the daemon is not connected")
                font.pixelSize: Theme.fontSizeMedium
                color: Theme.surfaceVariantText
                width: parent.width
            }

            Repeater {
                model: ScriptModel {
                    values: root.sections
                }

                Column {
                    id: section
                    required property var modelData
                    width: root.width
                    spacing: Theme.spacingS

                    StyledText {
                        text: section.modelData.label
                        font.pixelSize: Theme.fontSizeLarge
                        font.weight: Font.Medium
                        color: section.modelData.overdue ? Theme.error : Theme.surfaceText
                        width: parent.width
                    }

                    Repeater {
                        model: ScriptModel {
                            values: section.modelData.tasks
                        }

                        StyledRect {
                            id: card
                            required property var modelData
                            readonly property bool overdue: root.taskOverdue(modelData)
                            width: root.width
                            height: Math.max(60, contentRow.implicitHeight + Theme.spacingM * 2)
                            color: overdue ? Theme.withAlpha(Theme.error, 0.10) : Theme.surfaceContainer
                            border.color: overdue ? Theme.error : "transparent"
                            border.width: overdue ? 1 : 0
                            radius: Theme.cornerRadius

                            // Declared before contentRow so the checkbox's own
                            // StateLayer, on top, wins clicks in its area while
                            // this one handles the rest of the card.
                            StateLayer {
                                stateColor: Theme.primary
                                cornerRadius: parent.radius
                                onClicked: root.taskClicked(card.modelData)
                            }

                            Row {
                                id: contentRow
                                anchors.left: parent.left
                                anchors.right: parent.right
                                anchors.verticalCenter: parent.verticalCenter
                                anchors.leftMargin: Theme.spacingM
                                anchors.rightMargin: Theme.spacingM
                                spacing: Theme.spacingM

                                Rectangle {
                                    id: checkbox
                                    width: 22
                                    height: 22
                                    radius: 11
                                    anchors.verticalCenter: parent.verticalCenter
                                    color: card.modelData.completed ? card.modelData.color : "transparent"
                                    border.color: card.overdue ? Theme.error : card.modelData.color
                                    border.width: 2

                                    DankIcon {
                                        anchors.centerIn: parent
                                        name: "check"
                                        size: 14
                                        color: Theme.primaryText
                                        visible: card.modelData.completed
                                    }

                                    StateLayer {
                                        stateColor: card.modelData.color
                                        cornerRadius: parent.radius
                                        enabled: !card.modelData.readOnly
                                        onClicked: DankCalService.completeTaskWithUndo(card.modelData)
                                    }
                                }

                                Column {
                                    anchors.verticalCenter: parent.verticalCenter
                                    width: parent.width - 22 - priorityFlag.width - Theme.spacingM * 2
                                    spacing: Theme.spacingXS

                                    StyledText {
                                        text: card.modelData.title
                                        font.pixelSize: Theme.fontSizeLarge
                                        font.weight: Font.Medium
                                        font.strikeout: card.modelData.completed
                                        color: card.modelData.completed ? Theme.surfaceVariantText : (card.overdue ? Theme.error : Theme.surfaceText)
                                        width: parent.width
                                        wrapMode: Text.WordWrap
                                        maximumLineCount: 2
                                        elide: Text.ElideRight
                                    }

                                    Row {
                                        spacing: Theme.spacingS

                                        StyledText {
                                            text: root.dueLabel(card.modelData)
                                            visible: text !== ""
                                            font.pixelSize: Theme.fontSizeSmall
                                            color: card.overdue ? Theme.error : Theme.surfaceVariantText
                                            anchors.verticalCenter: parent.verticalCenter
                                        }

                                        Rectangle {
                                            width: 8
                                            height: 8
                                            radius: 4
                                            anchors.verticalCenter: parent.verticalCenter
                                            color: card.modelData.color
                                        }

                                        StyledText {
                                            text: card.modelData.accountSummary !== "" ? card.modelData.calendar + "  ·  " + card.modelData.accountSummary : card.modelData.calendar
                                            font.pixelSize: Theme.fontSizeSmall
                                            color: Theme.surfaceVariantText
                                            anchors.verticalCenter: parent.verticalCenter
                                        }

                                        DankIcon {
                                            visible: card.modelData.recurring
                                            name: "repeat"
                                            size: Theme.fontSizeSmall
                                            color: Theme.surfaceVariantText
                                            anchors.verticalCenter: parent.verticalCenter
                                        }
                                    }
                                }

                                DankIcon {
                                    id: priorityFlag
                                    anchors.verticalCenter: parent.verticalCenter
                                    name: "flag"
                                    size: Theme.iconSize - 6
                                    color: Theme.error
                                    visible: card.modelData.priority > 0 && card.modelData.priority <= 4
                                    width: visible ? Theme.iconSize - 6 : 0
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}
